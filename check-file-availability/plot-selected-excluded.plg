# gnuplot -c plot-2.plg InRelease.dat InRelease

set style fill transparent solid 0.1 border

set xlabel "UTC time"
set xdata time
set timefmt "%Y%m%dT%H%M"
set title "Mirrors serving the file: ".ARG2

# https://stackoverflow.com/a/26067070/776208
set terminal push
set terminal unknown
plot ARG1 using 1:3
set terminal pop

plot ARG1 using 1:3  w l lw 2 title "selected", \
     ARG1 using 1:5  w l title "excluded: country only", \
     ARG1 using 1:6  w l title "excluded: health-check 404" linecolor rgb "red", \
     ARG1 using 1:7  w l title "excluded: file size mismatch", \
     ARG1 using 1:8  w l title "excluded: mod time mismatch", \
     ARG1 using 1:9  w l title "excluded: unreachable", \
     ARG1 using 1:10 w l title "excluded: other reason" linecolor rgb "green", \
     ARG1 using 1:($11 * GPVAL_Y_MAX) with filledcurves lt nodraw title "fallback"

pause mouse close
