## $ gnuplot -c plot-total.plg InRelease.dat InRelease

## Set defaults (override via -e)
if (!exists("png")) png=0

## Set output file to save to
if (png == 1) {
    set terminal png size 1900,950
    set output "plot-total-".ARG2.".png"
}

## Set x-axis as time
set xdata time
set timefmt "%Y%m%dT%H%M"
set xlabel "UTC time"

## Set graph title
set title "Mirrors serving the file: ".ARG2

## Plot data
plot ARG1 using 1:2 w l lw 2 title "total", \
     ARG1 using 1:3 w l title "returned", \
     ARG1 using 1:4 w l title "excluded"

## Keep gnuplot running
if (png == 0) {
    pause mouse close
}
