## $ gnuplot -c plot-selected-excluded.plg InRelease.dat InRelease

## Set defaults (override via -e)
if (!exists("png")) png=0

## Set output file to save too
if (png == 1) {
    set terminal png size 1900,950
    set output "plot-selected-excluded-".ARG2.".png"
}

## Set x-axis as time
set xdata time
set timefmt "%Y%m%dT%H%M"
set xlabel "UTC time"

## Set graph title
set title "Mirrors serving the file: ".ARG2

## First pass to set GPVAL_Y_MAX, so that we can then draw a
## semi-transparent, full height box when fallback is set.
## REF: https://stackoverflow.com/a/26067070/776208
set terminal push
set terminal unknown
plot ARG1 using 1:3
set terminal pop

## Set style for fallback
set style fill transparent solid 0.1 border

## Plot data
plot ARG1 using 1:3  w l lw 2 title "selected", \
     ARG1 using 1:5  w l title "excluded: country only", \
     ARG1 using 1:6  w l title "excluded: health-check 404", \
     ARG1 using 1:7  w l title "excluded: file size mismatch", \
     ARG1 using 1:8  w l title "excluded: mod time mismatch", \
     ARG1 using 1:9  w l title "excluded: status code", \
     ARG1 using 1:10  w l title "excluded: unreachable", \
     ARG1 using 1:11 w l title "excluded: other reason", \
     ARG1 using 1:($12 * GPVAL_Y_MAX) with filledcurves lt nodraw title "fallback"

## Keep gnuplot running
if (png == 0) {
    pause mouse close
}
